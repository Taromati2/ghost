//链接md5相同的文件以减小文件夹体积占用
//by steve02081504 for Taromati2
//
//警告：此辞书未完工，使用此辞书可能有未知风险
//
md5linker.get_table_index{
	_md5=_argv[0]
	_begin=0
	_end=md5linker.file_num-1
	_index=0
	if _md5 < md5linker.md5_list[0]{
		-1
		return
	}
	if _md5 > md5linker.md5_list[_end]{
		_end
		return
	}
	while _end > _begin{
		_index = TOINT((_end + _begin)/2)
		if _md5 > md5linker.md5_list[_index]{
			_begin = _index + 1
		}elseif _md5 < md5linker.md5_list[_index]{
			_end = _index - 1
			if _md5 == md5linker.md5_list[_end]{
				_end
				return
			}
		}else{
			_index
			return
		}
	}
	_begin - (_md5 < md5linker.md5_list[_begin])
}
md5linker.get_same_file{
	_md5=_argv[0]
	if md5linker.md5_list[md5linker.table_index] == _md5
		md5linker.file_list[md5linker.table_index]
}
md5linker.new_md5:void{
	md5linker.file_num++
	if md5linker.table_index == -1{
		md5linker.file_list=(_argv[0],md5linker.file_list)
		md5linker.md5_list=(_argv[1],md5linker.md5_list)
	}
	else{
		md5linker.file_list[md5linker.table_index],=_argv[0]
		md5linker.md5_list[md5linker.table_index],=_argv[1]
	}
}
md5linker.get_md5_of{
	FDIGEST(_argv[0])+FSIZE(_argv[0])
}
md5linker.file_checker:void{
	_file = _argv[0]
	_md5 = md5linker.get_md5_of(_file)
	md5linker.table_index=md5linker.get_table_index(_md5)
	_same_file = md5linker.get_same_file(_md5)
	if _same_file{
		FDEL(_file)
		MAKEFILELINK(_file,_same_file)
		md5linker.linked_num++
	}
	else{
		md5linker.new_md5(_file,_md5)
	}
}
md5linker.mapper:void{
	_t=FENUM(_argv[0])
	foreach _t;_i{
		if ISDIR(_argv[0]+'\'+_i) && _i != '\.git'
			md5linker.mapper(_argv[0]+'\'+_i)
		else{
			md5linker.file_checker(_argv[0]+'\'+_i)
		}
	}
}
md5linker.varinit:void{
	md5linker.file_list=IARRAY
	md5linker.md5_list=IARRAY
	md5linker.file_num=0
	md5linker.linked_num=0
}
md5linker.clearvar:void{
	ERASEALLVARBEGINAS('md5linker')
}
md5linker.main{//md5linker.main('..\..\shell')
	md5linker.varinit
	foreach _argv;_t{
		md5linker.mapper(_t)
	}
	md5linker.linked_num
	--
	md5linker.clearvar
}
