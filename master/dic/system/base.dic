OnTest{
	'\![sound,play,SOUND_PATH\shot.wav]'
}
do_nothing{}
NeedRewrite:void{}
Select.Cancel{
	if mode == '0'
		'\0\s[6]好的'
	elseif mode == '1'
		"\1\s[%(specialmode != 'mealtime')10]哦。\n"
	--
	BaseSelect.base(_argv)
}
Select.Thanks{
	if mode == '0'
		'\0\s[2]啊\w9…\w9…\n/
		\s[1]谢谢。\n/
		'
	elseif mode == '1'
		"\1\s[%(specialmode != 'mealtime')10]哦\w9，也好。\n"
	--
	BaseSelect.base(_argv)
}
BaseSelect.base{
	case _argv[0]{
		when 'with_sakura'
			_argv[1]
		when 'with_func'{
			_argv[0]=IARRAY
			_fname=_argv[0]
			_argv[0]=IARRAY
			EVAL("Select.%(_fname)(%_argv)")
		}
	}
}
Select.UnFinished{
	UnFinished
}
UnFinished{
	'\0\s[3]很遗憾\w9\w9，这是一个未完成的机能。\n/
	\1\s[12]该好好努力啦\w9\n/
	自己动手才能丰衣足食哦\w9\n/
	\0/
	\q[◇协助项目开发,OnUrlOpen,https://github.com/Taromati2/Taromati2#%E6%88%91%E8%AF%A5%E5%A6%82%E4%BD%95%E5%8F%82%E4%B8%8E%E5%BC%80%E5%8F%91]\n/
	\q[◇不了,Cancel]\n/
	'
}
BUGNow{
	'\0\s[8]失败了...\n'
	--
	if _argv[0]
		'原因是：'+_argv[0]+'\n'
	else
		'未知bug吗?\n'
	--
	if _argv[1]
		'/
		\q[◇复制错误记录,OnCopy,'+SakuraScriptArgConverter(_argv[1])+']\n/
		\q[◇复制错误记录并提交issue,OnUrlOpen,'+SakuraScriptArgConverter('https://github.com/Taromati2/Taromati2/issues/new?labels=bug&template=bug-report.md&title='+_argv[0])+',with_copy,'+SakuraScriptArgConverter(_argv[1])+']\n/
		'
	else
		'\q[◇前往汇报,OnUrlOpen,https://github.com/Taromati2/Taromati2#bug%E5%8F%8D%E9%A6%88]\n'
	--
	'\q[◇不了,Cancel]\n'
}
OpenDocForRuntime.string{
	'\![open,browser,https://github.com/Taromati2/Taromati2/blob/master/docs/runtime/'+_argv[0]+'.md]'
}
OpenDocForRuntime{
	SHIORI_FW.PushNotifyResult(OpenDocForRuntime.string(_argv[0]))
}
