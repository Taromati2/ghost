//----------------------称呼-----------------------------------------
//first time
OnGetUsernameFirst{
	_name = reference.raw[0]
	if OnUserName.IsMazo || OnUserName.IsTooClose {
		"\0\s[2]\w9欸\w9.\w9.\w9.\s[8]那样的称呼、\w8我做不到。\w9\w9\n/
		如果可以的话、\w8请让我用其他的称呼方式吧。\n/
		\q[◇跳过,Cancel,with_sakura,%(SakuraScriptArgConverter('\![close,inputbox,OnGetUsernameFirst]'))]/
		\![set,balloontimeout,0]\![open,inputbox,OnGetUsernameFirst,-1,%(SakuraScriptArgConverter(username))]"
	}
	if !_name{
		if username
			"那么日后就称呼为“%(username)”了哦。"
		else
			'\0\s[6]这样啊…\w9\w9\s[100]\n那请您想说时再告诉我喔。'
		--
		OnGetUsernameFirst.End
	}
	elseif _t = OnUserName.SpecialCheck {
		OnUserName.Set(_name)
		"\0\s[0]『%(username)』是吗。"
		--
		_t
		--
		OnGetUsernameFirst.End
	}
	else{
		OnUserName.Set(_name)
		"\0\s[0]『%(username)君』\w4这样可以吗？\n\n\q[◇可以,OnGetUsernameFirst.Addkeigo]\n\q[%(SakuraScriptArgConverter('◇『'+username+'』就行了')),OnGetUsernameFirst.Fin]"
	}
}
OnGetUsernameFirst.Addkeigo{
	OnUserName.Set("%(username)君")
	--
	OnGetUsernameFirst.Fin
}
OnGetUsernameFirst.Fin{
	"\0\s[0]『%(username)』是吗。"
	--
	OnGetUsernameFirst.End
}
OnGetUsernameFirst.End{
	'\w9\w9\w9\n\n[half]\0\s[6]那么再一次…\w9\s[26]\n\n[half]橘花和斗和、以后请多多指教。\1\s[10]多指教啦。\w9\0\s[30]\n\n[half]…终于开口了。\w9\w9\c'
}

//common
OnUserNameChange{
	"\0\s[26]那么、该怎么称呼呢？\![open,inputbox,OnUserNameInput,-1,%(SakuraScriptArgConverter(username))]"
}
OnUserNameInput{
	_name = reference.raw[0]
	/*
	{
		_simplified_name = TOLOWER(TOSIMPLIFIED(_name))
		case _simplified_name{
			when '橘花','kikka'
			when '斗和','towa'
		}
	}
	*/
	if _name == username {
		'\0\s[0]………？\s[26]\n不用改变也可以吗？\n\n[half]/
		\q[◇返回上一层,OnOpenMenu,4]\n/
		\q[◇终了,OptionEnd]'
	}
	elseif !_name {
		"\0\s[6]好的。\w9\s[0]\n那么、往后就用\w9\s[26]\n『无名氏』\w9来称呼喔。\w9\w9\n\n[half]/
		\s[5]…\w9开玩笑的。\n/
		\w9\s[0]\n继续称呼『%(username)』就可以了吗\w9？\n[150]/
		\q[◇返回上一层,OnOpenMenu,4]\n/
		\q[◇终了,OptionEnd]"
	}
	elseif INARRAY(UsualUserName,_name){
		_index=ARRAY.GetIndex(UsualUserName,_name)
		//_longest=ARRAY.GetlongestContinuousPresence(UsualUserName,_name)
		_size=ARRAYSIZE(UsualUserName)
		_month=_size-_index
		_year=_month/12
		//if _longest > 13{

		//}
		if _name==UsualUserName[_size-1]
			"\0\s[0]%(_name).\w9.\w9.\w9\n/
			\s[26]果然还是叫回来更顺口呢？"
		elseif _year
			"\0\s[0]%(_name).\w9.\w9.\w9\n/
			\s[21]让我回想起了%(_year)年前呢，\s[6]那段时光\w9\n/
			\1\s[11]什么，不都是混吃等死吗。\w9\w9\n/
			\0\s[32]\1\s[13]\0/
			.\w9.\w9.\w9"
		elseif _month
			"\0\s[0]%(_name).\w9.\w9.\w9\n/
			\s[21]让我回想起了%(_month)月前呢，\s[6]那段时光\w9\n/
			\1\s[11]什么，不都是混吃等死吗。\w9\w9\n/
			\0\s[32]\1\s[13]\0/
			.\w9.\w9.\w9"
		--
		OnUserName.Set(_name)
		--
		'\n/
		\q[◇返回上一层,OnOpenMenu,4]\n/
		\q[◇终了,OptionEnd]'
	}
	elseif _t = OnUserName.SpecialCheck {
		if OnUserName.IsTooClose && 亲密度<100
			'\0\s[2]\w9欸\w9.\w9.\w9.\s[8]那样的称呼、\w8我做不到。\w9\w9\n/
			如果可以的话、\w8请让我用其他的称呼方式吧。\n/
			'
		else{
			OnUserName.Set(_name)
			_t
		}
		--
		'\q[◇返回上一层,OnOpenMenu,4]\n/
		\q[◇终了,OptionEnd]'
	}
	elseif OnUserName.IsMazo {
		if 亲密度>=100{
			"\0\s[2]\w9欸\w9.\w9.\w9.\s[8]真的想让我这样称呼你吗？\w9不是在开玩笑吗？\w9\w9\n/
			…\w9\s[6]如果你想被这样称呼的话、\w8也不是不可以。\w9\n/
			\s[0]但是、\w8\s[26]请不要讨厌我哦。\n/
			\n/
			\q[◇确认,OnUserNameConfirmed,%(SakuraScriptArgConverter(_name))]\n/
			\q[◇还是算了,OnUserNameCancel]"
		}else{
			'\0\s[2]\w9欸\w9.\w9.\w9.\s[8]那样的称呼、\w8我做不到。\w9\w9\n/
			如果可以的话、\w8请让我用其他的称呼方式吧。\n/
			'
			--
			'\q[◇返回上一层,OnOpenMenu,4]\n/
			\q[◇终了,OptionEnd]'
		}
	}
	else{
		"\0\s[26]『%(REPLACE(_name,'\','\\'))君』\w4这样可以吗？\n\n/
		\q[◇可以,OnUserNameConfirmed,%(SakuraScriptArgConverter(_name+'君'))]\n/
		\q[%(SakuraScriptArgConverter('◇『'+_name+'』就行了')),OnUserNameConfirmed,%(SakuraScriptArgConverter(_name))]\n\n[half]/
		\q[◇还是算了,OnUserNameCancel]"
	}
}
OnUserNameCancel{
	"\0\s[0]那么、仍然叫『%(username)』…\n[150]"
	"\0\s[6]好的。\w9\s[0]\n那么、就跟目前一样用\w9\s[26]\n『%(username)』\w9来称呼喔。\n[150]"
	--
	'\q[◇返回上一层,OnOpenMenu,4]\n/
	\q[◇终了,OptionEnd]'
}
OnUserNameConfirmed{
	_name = reference.raw[0]
	OnUserName.Set(_name)
	"\0\s[6]好的。\w9\s[0]\n\n[half]那么、往后就用\w9\s[26]\n『%(username)』\w9来称呼喔。\n[150]"
	--
	'\q[◇返回上一层,OnOpenMenu,4]\n/
	\q[◇终了,OptionEnd]'
}

//base
OnUserName.Set:void{
	username = REPLACE(_argv[0],'\','\\')
}
OnUserName.IsMazo{
	_name = reference.raw[0]
	_simplified_name = TOLOWER(TOSIMPLIFIED(_name))
	case _simplified_name {
		when '垃圾','豚','猪','垃圾虫','クズ','クソムシ','笨蛋','白痴','呆子','蠢猪','傻逼','废物','蠢货','阳痿','阳痿男','丑男','猪头男','猪头','二愣子','二傻子','弱智','脑残'{
			1
			return
		}
	}
	0
}
OnUserName.IsTooClose{
	_name = reference.raw[0]
	_simplified_name = TOLOWER(TOSIMPLIFIED(_name))
	case _simplified_name {
		when '亲爱的','老公','老公大人','相公','宝贝','爸爸','父','父亲','爸','マスター','主人大人','主人','ごしゅじんさま','honey','だぁりん','宝宝','小宝宝','baby','little baby','darling'{
			1
			return
		}
	}
	0
}
OnUserName.SpecialCheck{
	_name = reference.raw[0]
	if _t = OnUserName.SpecialCheck.base {
		"\0\s[6]好的。\w9\s[0]\n\n[half]那么、往后就用\w9\s[26]\n『%(_name)』\w9来称呼喔。\n[150]\w9\w9"
		--
		_t
		--
		'\w9\0\n\n'
	}
}
OnUserName.SpecialCheck.base{
	_name = reference.raw[0]
	_simplified_name = TOLOWER(TOSIMPLIFIED(_name))
	case _simplified_name {
		when '哥哥','大哥哥','哥','兄长大人','欧尼酱','にぃに','にぃ','兄さま','兄様','あに様'
			"\s[3]\w9我、\w9虽然没有陪我长大的兄弟姐妹。\w9\s[6]\w9但是你能像疼爱妹妹一样疼爱我，\w9真的很开心。\w9\w9\n/
			\s[1]\w9\w9%(_name)。"
		when '亲爱的'
			'\s[21]亲・爱・的…♡。'
		when '老公','老公大人','相公','宝贝'
			"\s[21]%(_name)…♡"
		when '爸爸','父','父亲','爸'
			'\s[8]已经把我当成女儿了吗。\n/
			\1\s[11]\w9呦，\w9可喜可贺可喜可贺\w9\n/
			\0\s[32]\w9.\w9.\w9.\1\4'
		when '医生','医师','大夫','郎中','センセー'
			'\1\s[12]喂，\w9这可不是TeachingF⭕⭕⭕ing啊，\w9给我注意点。\w9\w9\0\s[8]什么啊？'
		when '先辈','前辈','センパイ'
			'\s[20]…是做什麽的前辈呢？'
		when 'マスター','主人大人','主人','ごしゅじんさま'
			'\s[21]主仆play？\1\s[11]\w9.\w9.\w9.你在期待些什么啊。\w9'
		when '客人','客户','客官'
			'\s[8]…听起来怪怪的？'
		when '经纪人','Pさん','プロデューサーさん'
			'\s[8]…偶像？\1\s[12]？\0很抱歉、我不是很清楚这些'
		when '爷爷','爷','大爷'
			'\s[8]感觉把你称呼地这么老是不是不太好呢…。'
		when 'honey','だぁりん','darling'
			'\s[1]爱人…\w9\1在街上的话，\w9叫的出来吗。'
		when '博士','教授','はかせ','きょうじゅ'
			'\s[2]…做什么研究的吗？'
		when '队长','captain'
			'\s[20]难道、曾经在军队里呆过什么的…？'
		when '宝宝','小宝宝','baby','little baby'
			'\1意外的，\w9最近流行母性萝莉这种存在啊。\w9\w9\n/
			\0\s[8]欸？\w9\w9\n/
			\1\s[11]你有母性吗？\w9\w9\w9\n/
			\0就算这样说\w9.\w9.\w9.'
		//when '菊花'
			//''
		when '用户名','username'
			'\1\s[12]上一次听到这样的名字还是在上一次\w9\0\s[8]？'
		when '索娜·马萨卡','马萨卡','nani','娜妮'
			'\1\s[13]草\w9\0\s[8]？'
	}
}

