//////////////////////////////////////////////////
//Internet相关辞书
//written by forjane, 2009.12.03
//////////////////////////////////////////////////
TextBetween{
	_s = _argv[0]
	_begin = _argv[1]
	_end = _argv[2]
	_idx = 0
	if _argc > 3 {
		_idx = _argv[3]
	}
	_d = _s[_idx + 1,_begin]
	_d = _d[0,_end]
	_d
}
/*
TextBetween2{
	_s = _argv[0]
	_begin = _argv[1]
	_end = _argv[2]
	_idx = 0
	if _argc > 3 {
		_idx = _argv[3]
	}
	_d = _s[_idx,_end]
	_d = _d[1,_begin]
	_d
}
*/
TextToEnd{//TextToEnd('a=b=1234','=',0)=='b=1234'得到字符串'a=b=1234'中从第1个'='到字符串结束的文本'b=1234'
	_s=TOSTR(_argv[0])
	_begin=_argv[1]
	_idx = 0
	if _argc > 2 {
		_idx = TOINT(_argv[2])
	}
	_d = _s[_idx,_begin]
	while _s[_idx+1,_begin]!=''{
		_d+=_begin+_s[_idx+1,_begin]
		_idx++
	}
	if SUBSTR(_s,STRLEN(_s)-1,1)==_begin
		_d+=_begin
	_d
}
BeginToEnd{//BeginToEnd('君不见，黄河之水天上来，奔流到海不复回。','，',1,2)得到第一个'，'到第二个'，'之间的文本
	_s=_argv[0]
	_txt=_argv[1]
	_begin=TOINT(_argv[2])
	_end=0
	if _argc > 2 {
		_end=TOINT(_argv[3])
	}
	_d = _s[_begin,_txt]
	_idx=_begin+1
	while _s[_idx,_txt]&&(_idx<_end&&_end!=0||_end==0){
		_d+=_txt+_s[_idx,_txt]
		_idx++
	}
	_d
}
//图灵api
////////////////////////////////////////////////////////////////////////////////
OntulingApi{
	callingtulingapi=1
	tulingapi.in=_argv[0]
	"\![excute,http-get,http://www.tuling123.com/openapi/api?key=5b56e59d852489bb6c40289223619a28&info=%(tulingapi.in),--param-charset=UTF-8,--nofile,--timeout=200,--async=tulingapi]"
}
/*
code  说明
100000  文本类数据  text   {"code":100000,"text":"你也好啊"}
200000  网址类数据  text+url
301000  小说
302000  新闻
304000  应用、软件、下载
305000  列车
306000  航班
307000  团购
308000  优惠
309000  酒店
310000  彩票
311000  价格
312000  餐厅
*/
OnTuLingFound.Failure{
	_txt=KikkaTalkToUser(tulingapi.in)
	ERASEVAR('tulingapi.in')
	if chatroom{
		chatroomtxt+="%(_txt)#"
		OnChatRoom(j)+WriteChatroom
		return
	}
	_txt
}
OnTuLingFound{
	_txt='\b[2]\_q'
	{
		_buff = reference3
		_code=TextBetween(_buff,':',',',0)
		case _code{
			when '100000'
				_txt+=TextBetween(_buff,'":"','"}',0)
			when '200000'
				_txt+=TextBetween(_buff,'":"','",',0)+"   \j["+TextBetween(_buff,'":"','"}',1)+"]"+"\_a[OnUrl,"+TextBetween(_buff,'":"','"}',1)+"]"+TextBetween(_buff,'":"','"}',1)+"\_a"
			when '304000'{
				_txt+=TextBetween(_buff,'":"','",',0)+'\n'
				_t=TextBetween(_buff,'"list":',']')
				SETDELIM(_t,',')
				foreach _t;_temp{
					_txt+=TextBetween(_temp,'"name":','","',0)+TextBetween(_temp,'count":"','","',0)+"\_a[OnUrl,"+TextBetween(_temp,'"icon":','","',0)+"]"+TextBetween(_temp,'"icon":','","',0)+"\_a"+"\_a[OnUrl,"+TextBetween(_temp,'detailurl":"','"}',0)+"]"+TextBetween(_temp,'detailurl":"','"}',0)+"\_a"+'\n'
				}
			}
			when '40004'
				_txt=''
			others
				_txt+=TextBetween(_buff,'":"','"}',0)
		}
	}
	if _txt && tulingapi.in{
		_txt=REPLACE(_txt,'\b[2]\_q','')
		AITalkAdd('PSEUDO_AI_PATH\tuling.ai',tulingapi.in+'*'+_txt+'§')
		void FCOPY('PSEUDO_AI_PATH\tuling.ai','PROFILE_PATH')
	}
	if !_txt
		_txt=KikkaTalkToUser(tulingapi.in)
	ERASEVAR('tulingapi.in')
	if chatroom{
		chatroomtxt+="%(_txt)#"
		OnChatRoom(j)+WriteChatroom
		return
	}
	'\'+mode
	--
	_txt
}
//天气查询
////////////////////////////////////////////////////////////////////////////////
OnCheckWeather{
	if ISVAR('weather.lastcity') {
		OnWeatherUpdate.menu
		--
		"\b[1]『\_a[OnChangeCity]◇%(weather.lastcity)\_a』的天气：\n/
		今天的天气：\n/
		白天 %(weather.day)  %(weather.daytemp)℃\n/
		夜间 %(weather.night)  %(weather.nighttemp)℃\n/
		\n/
		明天的天气：\n/
		白天 %(weather.tomorrow.day)  %(weather.tomorrow.daytemp)℃\n/
		夜间 %(weather.tomorrow.night)  %(weather.tomorrow.nighttemp)℃\n/
		\n/
		"
		--
		'\q[◇返回,OnOpenMenu]    \q[◇取消,Cancel]\_q'
	}
	elseif ISVAR('city'){
		OnWeatherUpdate.menu
	}
	else{
		OnChangeCity
	}
}
OnChangeCity{
	"请输入你要查询的城市名称（留空以自动推断）：\![open,inputbox,OnChangeCityComplete,-1,%(city)]"
}
OnChangeCityComplete{
	city = reference0
	OnWeatherUpdate
}
OnWeatherUpdate.DosntNeedCheck{
	if weather.lasttime{
		_ymd_str=weather.lasttime[0,' ']
		_hms_str=weather.lasttime[0,' ']
		_year=TOINT(_ymd_str[0,'-'])
		_month=TOINT(_ymd_str[1,'-'])
		_day=TOINT(_ymd_str[2,'-'][0,' '])
		_hour=TOINT(_hms_str[0,':'])
		_min=TOINT(_hms_str[1,':'])
		_sec=TOINT(_hms_str[2,':'])
		if (GETSECCOUNT-GETSECCOUNT(_year,_month,_day,_hour,_min,_sec))<10800{
			1
			return
		}
	}
	0
}
OnWeatherUpdate{
	if !OnWeatherUpdate.DosntNeedCheck
		"\![execute,http-get,https://query.asilu.com/weather/gaode,--param=city=%(city),--param-charset=UTF-8,--nofile,--async=weather]"
}
OnWeatherUpdate.menu{
	FromWeatherUpdate.menu=1
	OnWeatherUpdate
}
OnWeatherUpdate.boot{
	FromWeatherUpdate.boot=1
	OnWeatherUpdate
}
OnWeatherUpdate.Success{
	//str for test
	_str=reference3
	//get city
	_city=TextBetween(_str,'city":"','",',0)
	if _city{
		weather.lastcity=_city
		if !ISVAR('city'){
			city=weather.lastcity
		}
		//get last update time
		weather.lasttime=TextBetween(_str,'reporttime":"','",',0)
		//get weather
		weather.day=TextBetween(_str,'dayweather":"','",',0)
		weather.night=TextBetween(_str,'nightweather":"','",',0)
		weather.daytemp=TextBetween(_str,'daytemp":"','",',0)
		weather.nighttemp=TextBetween(_str,'nighttemp":"','",',0)
		//get tomorrow weather
		weather.tomorrow.day=TextBetween(_str,'dayweather":"','",',1)
		weather.tomorrow.night=TextBetween(_str,'nightweather":"','",',1)
		weather.tomorrow.daytemp=TextBetween(_str,'daytemp":"','",',1)
		weather.tomorrow.nighttemp=TextBetween(_str,'nighttemp":"','",',1)

		if FromWeatherUpdate.menu{
			ERASEVAR('FromWeatherUpdate.menu')
			OnCheckWeather
		}
		elseif FromWeatherUpdate.boot{
			ERASEVAR('FromWeatherUpdate.boot')
			Weather.Talks
		}
	}
	else{
		ERASEVAR('city')
		OnWeatherUpdate
	}
}
OnWeatherUpdate.Failure{
	if FromWeatherUpdate.menu{
		ERASEVAR('FromWeatherUpdate.menu')
		'\0\s[8]查询失败，\w9检查下网络连接吧？'
	}
	elseif FromWeatherUpdate.boot{
		ERASEVAR('FromWeatherUpdate.boot')
	}
}
Weather.Talks{
	if hour>=8&&hour<18{
		if '晴' _in_ weather.day{
			'天晴的话出去走走比较好哦。'
			'今天天气还好啦，因为是晴天呢。'
		}
		elseif '雨' _in_ weather.day{
			'今天下雨了吗？下雨出门记得带伞哦。'
			'今天是雨天，天气不好啊'
		}
		elseif '雪' _in_ weather.day{
			"%(username)，外面下雪了吗？"
			'今天下雪了吗，好好玩'
		}
		if TOINT(weather.daytemp)<16
			'今天天气比较冷，要注意保重身体哦。'
		elseif TOINT(weather.daytemp)>38
			'天气太热了，还是少呆在外面比较好。'
		elseif TOINT(weather.daytemp)>33
			'今天天气比较热，要注意降温解暑哦。'
	}
	else{
		if '雨' _in_ weather.night
			'晚上可能有雨，记得收衣服关好门窗哦。'
		elseif '雪' _in_ weather.night
			'外面应该很冷吧，还是呆在家里比较好。'
		if TOINT(weather.nighttemp)<10
			'晚上睡觉要注意保暖哦，不要冻着身子。'
	}
}
Weather.EmbeddingString1{
	if weather.day{
		if hour>=8&&hour<18
			"天气：%(weather.day)  温度：%(weather.daytemp)℃"
		else
			"天气：%(weather.night)  温度：%(weather.nighttemp)℃"
	}
}
Weather.EmbeddingString2{
	if weather.day{
		if hour>=8&&hour<18
			"%(weather.lastcity) %(weather.day) "
		else
			"%(weather.lastcity) %(weather.night) "
		--
		"%(weather.daytemp)℃～%(weather.nighttemp)℃"
	}
}
//Hitokoto
OnHitokotoUpdate{
	'\![execute,http-get,https://v1.hitokoto.cn/,encode=text,--nofile,--async=hitokoto]'//更新一言
}
OnHitokotoUpdate.Success{
	hitokoto=reference3
	--
	DiscordCustomSet.CustomDetail(hitokoto)
	--
	DiscordCustomSet.CustomState('')
}
OnHitokotoUpdate.Failure{
	//hitokoto=''
}
ClearHitokotoVar{
	ERASEVAR('hitokoto')
}
//RSS读取
//////////////////////////////////////////////////////////////////////
/*
GetHeadlineList{
	_txt = ''
	_count = 0
	_list = IARRAY
	_path = FENUM('BASE_PATH\\..\\..\\..\\..\\headline')
	_path = REPLACE(_path,'\','\\')
	foreach _path; _i {
		_list[_count] = 'BASE_PATH\\..\\..\\..\\..\\headline' + _i + '\\'
		_count ++
	}
	TOSTR(_list)
}
ReadXml{
	_file = _argv[0]
	_buff = ''
	_txt = '\_q\b[2]'
	_title = ''
	_url = ''
	if FOPEN(_file,'r') {
		for _buff = FREAD(_file); _buff != -1; _buff = FREAD(_file) {
			if '<item' _in_ _buff {
				_title = ''
				_url = ''
				_endofitem = 0
				while _endofitem != 1 {
					if '<title>' _in_ _buff {
						while '</title>' !_in_ _buff {
							_buff += FREAD(_file)
						}
						_tmp = _buff[1,'<title>']
						_tmp = _tmp[0,'</title>']
						_title = _tmp
					}
					if '<link>' _in_ _buff {
						while '</link>' !_in_ _buff {
							_buff += FREAD(_file)
						}
						_tmp = _buff[1,'<link>']
						_tmp = _tmp[0,'</link>']
						_url = _tmp
					}
					if '</item>' _in_ _buff {
						_endofitem = 1
						if _url != '' {
							_txt += '\_a[OnUrl,' + _url + ']' + _title + '\_a\n'
						}
						else {
							_txt += _title + '\n'
						}
					}
					else {
						_buff = FREAD(_file)
					}
				}
			}
		}
	}
	FCLOSE(_file)
	_txt = REPLACE(_txt,'&quot;','"')
	_txt = REPLACE(_txt,'&amp;','&')
	_txt = REPLACE(_txt,'&nbsp;',' ')
	_txt = REPLACE(_txt,'&lt;','<')
	_txt = REPLACE(_txt,'&gt;','>')
	_txt = REPLACE(_txt,'<br />',' ')
	_txt
}
SendTweet{
	_username = 'forjanezz'
	_password = '111111'
	_message = 'http://aaa.bbb.ccc'
	_url = "http://%(_username):%(_password)@twitter.com/statuses/update.xml"
	"\_q%(_url)\![execute,http-post,%(_url),--param=status=%(_message),--param-charset=UTF-8,--file=twitter.xml,--timeout=200,--async=twitterpost]"
}
*/
OnAstrofate{
	if QuickStartVersion{
		NeedUpdate
		return
	}
	_submenu = 0
	_date='day'
	_astro='aries'
	if _argc > 0 {
		_submenu = _argv[0]
		_date=_argv[1]
		_astro=_argv[2]
	}
	if ISVAR('submenu'){
		_submenu=submenu
		_date=date
		_astro=astro
		ERASEVAR('submenu')
		ERASEVAR('date')
		ERASEVAR('astro')
	}
	_sub = '◆,◇,◇,◇,◇,◆,◇,◇,◇,◇,◆,◇,◇,◇,◇,◆,◇,◇,◇,◇,◆'
	"\C\![quicksession,true]\c\![set,choicetimeout,0]\b[2]\f[color,255,158,158]\q[%(_sub[_submenu])每日运势,OnDownLoadAstrofate,0,day,%(_astro)]/
	\_l[60]\q[%(_sub[_submenu + 4])每周运势,OnDownLoadAstrofate,1,week,%(_astro)]/
	\_l[120]\q[%(_sub[_submenu + 8])每月运势,OnDownLoadAstrofate,2,month,%(_astro)]/
	\_l[180]\q[%(_sub[_submenu + 12])年度运势,OnDownLoadAstrofate,3,year,%(_astro)]/
	\_l[240]\q[%(_sub[_submenu + 16])年度爱情,OnDownLoadAstrofate,4,year_love,%(_astro)]\n/
	"
	--
	"点击星座查看运程\f[color,default]\n/
	\f[color,216,67,179]\_l[0]\q[◇牧羊座,OnDownLoadAstrofate,%(_submenu),%(_date),Aries]/
	\_l[50]\q[◇金牛座,OnDownLoadAstrofate,%(_submenu),%(_date),Taurus]/
	\_l[100]\q[◇双子座,OnDownLoadAstrofate,%(_submenu),%(_date),Gemini]/
	\_l[150]\q[◇巨蟹座,OnDownLoadAstrofate,%(_submenu),%(_date),Cancer]/
	\_l[200]\q[◇狮子座,OnDownLoadAstrofate,%(_submenu),%(_date),Leo]/
	\_l[250]\q[◇处女座,OnDownLoadAstrofate,%(_submenu),%(_date),Virgo]\n/
	\_l[0]\q[◇天秤座,OnDownLoadAstrofate,%(_submenu),%(_date),Libra]/
	\_l[50]\q[◇天蝎座,OnDownLoadAstrofate,%(_submenu),%(_date),Scorpio]/
	\_l[100]\q[◇射手座,OnDownLoadAstrofate,%(_submenu),%(_date),Sagittarius]/
	\_l[150]\q[◇摩羯座,OnDownLoadAstrofate,%(_submenu),%(_date),Capricorn]/
	\_l[200]\q[◇水瓶座,OnDownLoadAstrofate,%(_submenu),%(_date),Aquarius]/
	\_l[250]\q[◇双鱼座,OnDownLoadAstrofate,%(_submenu),%(_date),Pisces]\f[color,default]\n"
	--
	ShowAstroFate(_submenu,_date,_astro)
}
GetAstroName{
	_month = TOINT(reference0)
	_day = TOINT(reference1)
	if _argc>0{
		_month = TOINT(_argv[0])
		_day = TOINT(_argv[1])
	}
	if (_month == 3 && _day >=21 ) || (_month == 4 && _day <= 20){ 'Aries'}
	elseif (_month == 4 && _day >=20 ) || (_month ==5 && _day <= 20){ 'Taurus'}
	elseif (_month == 5 && _day >=21 ) || (_month ==6 && _day <= 21){ 'Gemini'}
	elseif (_month == 6 && _day >=22 ) || (_month == 7 && _day <= 22){ 'Cancer'}
	elseif (_month == 7 && _day >=23 ) || (_month == 8 && _day <= 22){ 'Leo'}
	elseif (_month == 8 && _day >=23 ) || (_month == 9 && _day <= 22){ 'Virgo'}
	elseif (_month == 9 && _day >=23 ) || (_month == 10 && _day <= 23){ 'Libra'}
	elseif (_month == 10 && _day >=24 ) || (_month == 11 && _day <= 22){ 'Scorpio'}
	elseif (_month == 11 && _day >=23 ) || (_month == 12 && _day <= 21){ 'Sagittarius'}
	elseif (_month == 12 && _day >=22 ) || (_month == 1 && _day <= 19){ 'Capricorn'}
	elseif (_month == 1 && _day >=20 ) || (_month == 2 && _day <= 18){ 'Aquarius'}
	elseif (_month == 2 && _day >=19 ) || (_month == 3 && _day <= 20){ 'Pisces'}
}
OnAstroSubMenu{
	OnAstrofate(reference0,reference1,reference2)
}
OnAstroFoundAuto{
	if ISVAR('birthday'){
		date='day'
		astro=GetAstroName(birthday[1],birthday[2])
		submenu=0
		"\0\b[-1]\![excute,http-get,http://astro.sina.com.cn/fate_%(date)_%(astro)/,--param-charset=UTF-8,--file=astro-sina.html,--timeout=200,--async=OnAstroFateAuto]"
	}
}
OnAstroFateAuto{
	_txt='\_q\![set,autoscoll,false]'
	_txt+="今日%(username)的运势如下：\n"
	_date='day'
	_file = 'VAR_PATH\\astro-sina.html'
	_found=0
	if FOPEN(_file,'r'){
		for _buff = FREAD(_file); _buff != -1; _buff = FREAD(_file){
			_table=0
			for _tmp = FREAD(_file);  _tmp != -1; _tmp = FREAD(_file){
				_buff += _tmp
				if '<h4>' _in_ _tmp{
					_table++
				}
			}
			if TextBetween(_buff,'<li class="datea">','</li>')!=""
				_txt+='\f[color,255,158,158]'+TextBetween(_buff,'<li class="datea">','</li>')+'\f[color,default]'+'\n'//今日运势有效日期
			_month=TextBetween(_buff,'<li class="datea">','</li>')[1,'-']
			_day=TextBetween(_buff,'<li class="datea">','</li>')[2,'-']
			_txt+='\f[color,108,129,182]'+TextBetween(_buff,'<span>','<em>')+"（%(astro)）"+'\n'+TextBetween(_buff,'<em>','</em>')+'\f[color,default]'+'\n'//星座名
			if TOINT(_month)==TOINT(month)&&TOINT(_day)==TOINT(day)
				_found=1
			if TextBetween(_buff,'<li class="date">','</li>')!=""
				_txt+='\f[color,255,158,158]'+TextBetween(_buff,'<li class="date">','</li>')+'\f[color,default]'+'\n'//一周运势有效日期
			for _i = 0; _i < _table; _i ++{
				_tmp='\f[color,255,158,158]'+TextBetween(_buff,'<p>','</p>',_i)+'\f[color,default]'
				_star=''
				_j=1
				if 'http' _in_ _tmp{
					while _tmp[_j,'http']!=''{
						_star+='★'
						_j++
					}
				}
				if _star!=''
					_tmp='\f[color,108,129,182]'+_star+'\f[color,default]'
				_txt+='\f[color,108,129,182]'+TextBetween(_buff,'<h4>','<',_i)+'\f[color,default]'+'    '+ _tmp+'\n'
			}
			_txt=REPLACE(_txt,'<br />','\n')
			_txt=REPLACE(_txt,'\n\n','\n')
			_txt=REPLACE(_txt,'&nbsp;','')
		}
	}
	FCLOSE(_file)
	_txt+='\_q\x'
	if _found
		_txt
}
OnDownLoadAstrofate{
	submenu=reference0
	date=reference1
	astro=reference2
	_otherlink=reference3
	if _otherlink!=''
		"正在连接网站，请稍等……\![excute,http-get,%(_otherlink),--param-charset=UTF-8,--file=astro-sina.html,--timeout=200,--async=OnAstrofate]"
	else
		"正在连接网站，请稍等……\![excute,http-get,http://astro.sina.com.cn/fate_%(date)_%(astro)/,--param-charset=UTF-8,--file=astro-sina.html,--timeout=200,--async=OnAstrofate]"
}
ShowAstroFate{
	_submenu = 0
	_date='day'
	_astro='aries'
	if _argc > 0 {
		_submenu=_argv[0]
		_date=_argv[1]
		_astro=_argv[2]
	}
	_txt=''
	_file = 'VAR_PATH\\astro-sina.html'
	if FOPEN(_file,'r'){
		for _buff = FREAD(_file); _buff != -1; _buff = FREAD(_file){
			_table=0
			for _tmp = FREAD(_file);  _tmp != -1; _tmp = FREAD(_file){
				_buff += _tmp
				if '<tr>' _in_ _tmp{
					_table++
				}
			}
			_txt+='\f[color,108,129,182]'+TextBetween(_buff,'<div class="tit_n">','<')+"（%(_argv[2])）"+'\n'+TextBetween(_buff,'<div class="tit_d">','<')+'\f[color,default]'+'\n'//星座名
			_txt+='\f[color,255,158,158]'+TextBetween(_buff,'<div class="time">','</div>')+'\f[color,default]'+'\n'//今日运势有效日期
			_txt+='\f[color,255,158,158]'+TextBetween(_buff,'<li class="date">','</li>')+'\f[color,default]'+'\n'//一周运势有效日期
			_txt+=TextBetween(_buff,'<li class="notes">','</li>')+'\n'
			if _date=='day'||_date=='tomorrow'{
				_tmplink ="http://astro.sina.com.cn/fate_day_%(_astro)/"
				_txt+="\f[color,216,67,179]\q[◇今日运势,OnDownLoadAstrofate,%(_submenu),%(_date),%(_astro),%(_tmplink)]\f[color,default]\n"//今日运势
				_tmplink ="http://astro.sina.com.cn/fate_tomorrow_%(_astro)/"
				_txt+="\f[color,216,67,179]\q[◇明日运势,OnDownLoadAstrofate,%(_submenu),%(_date),%(_astro),%(_tmplink)]\f[color,default]\n[150]"//明日运势
			}
			for _i = 0; _i < _table; _i++ {//每日运势
				_tmp=TextBetween(_buff,'<tr>','</tr>',_i)
				_td='\f[color,255,158,158]  '+TextBetween(_tmp,'<td>','</td>',0)+'\f[color,default]'
				_tdclass='\f[color,255,158,158]'+TextBetween(_tmp,'<td class="tb_td1">','</td>',0)+'\f[color,default]'
				_tdpic=TextBetween(_tmp,'<td><i class="','"></i></td>',0)
				if _tdpic!=''
					_td="\_l[50]\_b[DATA_PATH/weather/%(_tdpic).png,inline,--option=opaque]"
				_txt+=_tdclass+_td
				_tdclass='\f[color,255,158,158]'+TextBetween(_tmp,'<td class="tb_td1">','</td>',1)+'\f[color,default]'
				_txt+='\_l[150]'
				_td='\f[color,255,158,158]  '+TextBetween(_tmp,'<td>','</td>',1)+'\f[color,default]'
				_tdpic=TextBetween(_tmp,'<td><i class="','"></i></td>',1)
				if _tdpic!=''
					_td="\_l[200]\_b[DATA_PATH/weather/%(_tdpic).png,inline,--option=opaque]\n[[50]"
				_txt+=_tdclass+_td
				_txt+='\n'
			}
			_tmp=TextBetween(_buff,'<div class="content clearfix">','<!-- /article html -->')
			_txt+='\n[50]'
			_k=0
			while TextBetween(_tmp,'<div class=','</div>',_k)!=''{//每周运势
				_temp=TextBetween(_tmp,'<div class=','</div>',_k)
				if TextBetween(_temp,'<span class="sp3">','</span>')!=""//名称
					_txt+='\f[color,108,129,182]'+TextBetween(_temp,'<span class="sp3">','</span>')+'\f[color,default]'+'\n'
				if TextBetween(_temp,'<span class="sp4">','</span>')!=""
					_txt+='\f[color,255,158,158]'+TextBetween(_temp,'<span class="sp4">','</span>')+'\f[color,default]'+'\n'
				if _temp[1,'words">']!=''&&'<p>' !_in_ _temp[1,'words">']
					_txt+='\f[color,255,158,158]'+_temp[1,'words">']+'\f[color,default]'+'\n'
				_l=0
				while TextBetween(_temp,'<p>','</p>',_l)!=''{
					_t=TextBetween(_temp,'<p>','</p>',_l)
					_txt+='\f[color,255,158,158]'+_t+'\f[color,default]'+'\n'
					_l++
				}
				_k++
			}
			_txt=REPLACE(_txt,'<strong>','\f[bold,true]')
			_txt=REPLACE(_txt,'</strong>','\f[bold,false]')
			if '<div class="lotconts">' _in_ _buff
				_txt+='\f[color,255,158,158]'+TextBetween(_buff,'<div class="lotconts">','</div>',0)+'\f[color,default]'+'\n'
			if TextBetween(_buff,'<div class="m_left">','</div>',0)!=""
				_txt+='\n'+'\f[color,255,158,158]'+TextBetween(_buff,'<div class="m_left">','</div>',0)+'\f[color,default]'+'\n'
			if TextBetween(_buff,'<div class="m_right">','</div>',0)!=""
				_txt+='\n'+'\f[color,255,158,158]'+TextBetween(_buff,'<div class="m_right">','</div>',0)+'\f[color,default]'+'\n'
			_txt=REPLACE(_txt,'&nbsp;','')
			_txt=REPLACE(_txt,'<br />','\n')
		}
	}
	FCLOSE(_file)
	_txt+='\n\f[color,255,158,158]\q[◇返回,OnOpenMenu,0]  \q[◇终了,]\f[color,default]'
	_txt
}
OnWikiPedia{
	'\C\![quicksession,true]\c\0\b[2]\![set,choicetimeout,0]/
	■维基百科■\n[150]/
	\q[◇搜索词条,OnInputPediaWord]\n'
	--
	ShowBaiduBaike
	--
	'\n\q[◇返回,OnOpenMenu,0]'
}
OnInputPediaWord{
	"\C\![open,inputbox,OnDownLoadWikiPedia,-1,%(word)]"
}
OnDownLoadWikiPedia{
	word=reference0
	"正在连接网站，请稍等……\![excute,http-get,http://zh.wikipedia.org/wiki/%(word),--param-charset=UTF-8,--file=wiki-pedia.html,--timeout=200,--async=OnWikiPedia]"
}
ShowBaiduBaike{
	UnFinished
	/*
	_file = 'VAR_PATH\\baidu-baike.html'
	if FOPEN(_file,'r'){
		for _buff = FREAD(_file); _buff != -1; _buff = FREAD(_file){
		}
	}
	*/
}
/////////////////////////////////////////////////////////////////////////////
OnUrl{
	"\C\j[%(reference0)]"
}
OnExecuteHTTPComplete{
	if reference1[0,'='] == 'rss' {
		_tmp = reference1[1,'=']
		//ReadXml("VAR_PATH\\%(_tmp)")
	}
	elseif reference1 == 'weather' {
		OnWeatherUpdate.Success
	}
	elseif reference1 == 'checkip' {
		OnIpFound
	}
	elseif reference1 == 'googlecal' {
		OnGoogleCalFound
	}
	elseif reference1 == 'tulingapi' {
		OnTuLingFound
	}
	elseif reference1 == 'kikkastock' {
		if kikkastock==1
			OnKikkaStockMenu
		elseif kikkastock==2
			OnKikkaStockSelect
		elseif kikkastock==3
			OnKikkaStockFound
	}
	elseif reference1 == 'hitokoto' {
		OnHitokotoUpdate.Success
	}
	else {
		BUGNow('OnExecuteHTTPComplete的未定义async id: '+reference1)
	}
}
OnExecuteHTTPFailure{
	if reference1 == 'weatherurl' || reference1 == 'weather' || reference1 == 'weather24' || reference1 == 'weatherig' {
		"\0\s[3]连线失败了……\nError：%(reference4)\n要查看本地信息吗？\n\q[◇确定,OnWeatherFound]"
		--
		'\n\n\q[◇返回,OnCheckWeather]    \q[◇终了,OnWeatherEnd]'
	}
	else {
		if reference1=='weather'{
			OnWeatherUpdate.Failure
		}
		elseif reference1 == 'tulingapi' {
			OnTuLingFound.Failure
		}
		elseif reference1 == 'hitokoto' {
			OnHitokotoUpdate.Failure
		}
		else{
			"\0\s[3]连线失败了……\nError：%(reference4)"
		}

	}
}
