/////////
/////////换装
/////////amend by KikkaAIdb.[2009-11-13]★
//**************************************
//要显示图片必须shell的名字和shell的文件夹名字一样,或者先切换一次该shell就可以了
//其实可以使用\![get,property,事件,currentghost.shelllist(外表名称).path],路径会在事件的reference0传回,不过如果外表名称中带有括号的话会无法实现...
//**************************************
//---- SHELL更替开始 -----------------------------------------------------------------
OnShellChanging{
	if NeedsUpdateSurfaceDump
		UpdateSurfaceDump
	--
	if IsHShell(reference0) && !CanH{
		"\0\s[7]真是的，%(username)都在想些什么？\n我们的关系还没有好到可以做H的事的程度吧\w[1000]"
		return
	}
	if judgeshell(reference0)!=judgehaveshell(reference0)&&judgehaveshell(reference0)!='-1'{
		SETDELIM(reference2,'/')
		_p=reference2[ARRAYSIZE(reference2)-2]
		Haveshellinfo+="%(reference0)|%(_p)*"
	}
	if judgeshell(reference0)=='0|'&& Userspitem[0][2]!=1{
		haveshell=0
		'\0\s[-1]\b[-1]'
	}
	else{
		haveshell=1
		OnChangeShellTalk(reference0)
	}
}
OnAutoChangeShell{
	if TOINT(AutoShellChange[1])==0&&month==AutoShellChangeflag[0]&&day==AutoShellChangeflag[1]//'每天初见更换'
		return
	elseif TOINT(AutoShellChange[1])==1//每次问好更换
		do_nothing
	elseif TOINT(AutoShellChange[1])==2{//每四小时更换
		if hour<AutoShellChangeflag[2]+4
			return
	}
	Lunartime
	SETDELIM(Haveshellinfo,'*')
	_shellid=''
	if GetPhase<1{
		OnSilentChangeShell('预设')
		return
	}
	if GetPhase==1 && shellID=='预设'{
		return
	}
	foreach Haveshellinfo;_i{
		_t=_i[0,'|']
		if lunartime[10] {
			case lunartime[10]{
				when '元旦'{
					if _t=='和服' || _t=='浴衣'{
						_shellid=_t+'|'
						break
					}
				}
				when '万圣节'{
					if _t=='万圣节'||_t=='ハロウィン'{
						_shellid=_t+'|'
						break
					}
				}
				when '圣诞节'{
					if _t=='圣诞服装'||_t=='聖誕服裝'{
						_shellid=_t+'|'
						break
					}
				}
			}
		}
		if month==2&&day==22{//猫咪日
			if _t=='猫耳'||_t=='猫耳装'{
				_shellid=_t+'|'
				break
			}
		}
		elseif month==3&&day==5{//巫女日
			if _t=='銀狐巫女'{
				_shellid=_t+'|'
				break
			}
		}
		elseif month==3&&day==9{//初音日
			if 'miku' _in_ _t{
				_shellid=_t+'|'
				break
			}
		}
		elseif month==3&&day==28||month==4&&day==29{//《胧村正》发行日
			if 'momohime' _in_ _t{
				_shellid=_t+'|'
				break
			}
		}
		elseif month==4&&day==8{//魂魄 妖梦生日
			if _t=='Youmu Konpaku'{//魂魄 妖梦
				_shellid=_t+'|'
				break
			}
		}
		elseif month==5&&day==2{//女仆日
			if _t=='女仆'||_t=='女仆装'||_t=='Sakuya Izayoi'{
				_shellid=_t+'|'
				break
			}
		}
		elseif month==6&&day==10||month==8&&day==11||month==9&&day==27{//东方红魔乡发售
			if _t=='Sakuya Izayoi'{//十六夜 咲夜
				_shellid=_t+'|'
				break
			}
		}
		elseif month==8&&day==17{//东方风神录发售
			if _t=='Momizi Inubashiri'{//犬走椛
				_shellid=_t+'|'
				break
			}
		}
		elseif month==9&&day==3{//小原节子生日
			if _t=='SETSUKO OHARA'{//小原节子 - 超级机器人大战Z
				_shellid=_t+'|'
				break
			}
		}
		elseif month==10&&day==13{//世界保键日
			if _t=='白衣护士'||_t=='護士服裝'||_t=='ナース'{
				_shellid=_t+'|'
				break
			}
		}
		elseif month==10&&day==22{//win7发行日
			if _t=='WIN7'{
				_shellid=_t+'|'
				break
			}
		}
		elseif month==10&&day==25{//湾娘生日
			if 'taiwan' _in_ _t{
				_shellid=_t+'|'
				break
			}
		}
		else{
			if month==3||month==4||month==5{
				if (_t=='传统衣着' || _t=='浴衣') && GetPhase>=2{
					_shellid+=_t+'|'
				}
				elseif _t=='体操服'{
					_shellid+=_t+'|'
				}
				elseif _t=='女背心'{
					_shellid+=_t+'|'
				}
				elseif _t=='MeguMegu'{
					_shellid+=_t+'|'
				}
				elseif _t=='キャミソール'{
					_shellid+=_t+'|'
				}
				elseif _t=='ワンピース'{
					_shellid+=_t+'|'
				}
				elseif _t=='黒ワンピ'{
					_shellid+=_t+'|'
				}
				elseif _t=='ドイツ艦（艦これ）'{
					_shellid+=_t+'|'
				}
				elseif _t=='预设'{
					_shellid+=_t+'|'
				}
			}
			elseif month==6||month==7||month==8{
				if (_t=='传统衣着' || _t=='浴衣') && GetPhase>=2{
					_shellid+=_t+'|'
				}
				elseif '夏服' _in_ _t{
					_shellid+=_t+'|'
				}
				elseif _t=='吊带裙-粉红' && GetPhase>=4{
					_shellid+=_t+'|'
				}
				elseif _t=='旗袍' && GetPhase>=3{
					_shellid+=_t+'|'
				}
				elseif _t=='ワンピース' && GetPhase>=1{
					_shellid+=_t+'|'
				}
				elseif _t=='黒ワンピ'{
					_shellid+=_t+'|'
				}
				elseif _t=='装甲削って高機動化'{
					_shellid+=_t+'|'
				}
				elseif _t=='膝隠して太腿隠さず'{
					_shellid+=_t+'|'
				}
				elseif _t=='キャミソール'{
					_shellid+=_t+'|'
				}
				elseif _t=='しましま水着' && GetPhase>=2{
					_shellid+=_t+'|'
				}
				elseif _t=='水玉水着' && GetPhase>=2{
					_shellid+=_t+'|'
				}
				elseif _t=='裸Yシャツ' && GetPhase>=4{
					_shellid+=_t+'|'
				}
				elseif _t=='预设'{
					_shellid+=_t+'|'
				}
			}
			elseif month==9||month==10||month==11{
				if (_t=='传统衣着' || _t=='浴衣') && GetPhase>=2{
					_shellid+=_t+'|'
				}
				elseif _t=='ワンピース' && GetPhase>=1{
					_shellid+=_t+'|'
				}
				elseif _t=='white base'{
					_shellid+=_t+'|'
				}
				elseif _t=='MeguMegu'{
					_shellid+=_t+'|'
				}
				elseif _t=='SETSUKO OHARA'{
					_shellid+=_t+'|'
				}
				elseif _t=='taiwan'{
					_shellid+=_t+'|'
				}
				elseif _t=='预设'{
					_shellid+=_t+'|'
				}
			}
			elseif month==12||month==1||month==2{
				if (_t=='传统衣着' || _t=='浴衣') && GetPhase>=2{
					_shellid+=_t+'|'
				}
				elseif _t=='毛衣'{
					_shellid+=_t+'|'
				}
				elseif _t=='犬耳装' && GetPhase>=2{
					_shellid+=_t+'|'
				}
				elseif _t=='猫耳装' && GetPhase>=2{
					_shellid+=_t+'|'
				}
				elseif _t=='长裙'{
					_shellid+=_t+'|'
				}
				elseif _t=='连身裙（紅）' && GetPhase>=2{
					_shellid+=_t+'|'
				}
				elseif _t=='Maetel'{
					_shellid+=_t+'|'
				}
				elseif _t=='预设'{
					_shellid+=_t+'|'
				}
			}
		}
	}
	if _shellid!=''{
		SilentShellChange=1
		SETDELIM(_shellid,'|')
		_n=ARRAYSIZE(_shellid)-1
		__shellid=_shellid[RAND(_n)]
		if _n>1{
			while __shellid==shellID{
				__shellid=_shellid[RAND(_n)]
			}
		}
		AutoShellChangeflag=(month,day,hour)
		if __shellid!=shellID{
			'\0\w9\w9\s[26]啊，\w8请稍等一下哦。\w9\w9\s[-1]\1\s[-1]'
			--
			OnSilentChangeShell(__shellid)
		}
	}
}
HaveShellCount{
	SETDELIM(Haveshellinfo,'*')
	MAX(ARRAYSIZE(Haveshellinfo)-1,0)
}
OnBuyShell{
	if money>=5000{
		haveshell=1
		money-=5000
		crystalnum=TOINT(crystalnum)+1
		ERASEVAR('SilentShellChange')
		"\1\s[10]\0\s[0]花费￥5000金钱激活%(reference0)。\w9\w9\x\![change,shell,%(reference0)]]"
	}
	else{
		'\1\s[10]\0\s[9]很遗憾，钱不够啊。\w9\w9\w9'
		--
		OnSilentChangeShell(reference1)
	}
}
OnDressupChanged{
	NeedsUpdateSurfaceDump=1
}
UpdateSurfaceDump{
	_shellID=shellID
	if _argv[0]
		_shellID=_argv[0]
	else
		ERASEVAR('NeedsUpdateSurfaceDump')
	if judgeshell(_shellID)!='0|'{
		void MKDIR('VAR_PATH\surface_dumps')
		_surface_dump_dir='VAR_PATH\surface_dumps\'+judgeshell(_shellID)
		void MKDIR(_surface_dump_dir)
		'\![execute,dumpsurface,'+_surface_dump_dir+',0,"0-3,5,7,20,21,22,23,26,30,32"]'
	}
}
HasSurfaceDump{
	_shellID=shellID
	if _argv[0]
		_shellID=_argv[0]
	_surface_dump_dir='VAR_PATH\surface_dumps\'+judgeshell(_shellID)
	FILEEXIST(_surface_dump_dir)
}
//---- SHELL更替完毕 -----------------------------------------------------------------
OnShellChanged{
	if IsHShell(reference0) && !CanH{
		if IsHShell(shellID)
			shellID='预设'
		"\0\s[-1]\![raise,OnSilentChangeShell,%(shellID)]"
		return
	}else{
		'\0\s[0]'
	}
	--
	_temp=shellID
	shellID = reference0
	if !HasSurfaceDump
		NeedsUpdateSurfaceDump=1
	--
	if AutoUpdate
		OnSilentUpdate.shell
	--
	if Userspitem[0][2]==1||judgehaveshell(_temp)!='-1'||judgeshell(_temp)!='0|'
		haveshell=1
	if SilentShellChange{
		ERASEVAR('SilentShellChange')
		return
	}
	if haveshell==1{
		if judgeshell(shellID)=='0|'{
			SETDELIM(reference2,'/')
			_p=reference2[ARRAYSIZE(reference2)-2]
			Haveshellinfo+="%(reference0)|%(_p)*"
			'\0\s[0]…嗯、\w9\w9\s[5]这样子如何呢？'
		}
		elseif bedroom == '1'{
			'\0\s[0]…嗯、\w9\w9\s[5]洗好了吗？\n\n\q[◇洗好了,Choice9]\n\q[◇没事了,Choice2]\x'
		}
		else{
			'\0\s[0]…嗯、\w9\w9\s[5]这样子如何呢？'
		}
	}
	elseif !haveshell {
		SilentShellChange=1
		"\1\s[10]\0\s[0]…嗯、\w9\w9橘花还没有激活这套衣服呢,要给橘花购买能量水晶激活吗?花费￥30000金钱.\n\n\q[◇是的,OnBuyShell,%(shellID),%(_temp)]\n\q[◇不了,OnSilentChangeShell,%(_temp)]"
	}
}
//**************************************************************************************************************
OnFittingRoom:all{
	if NeedsUpdateSurfaceDump
		UpdateSurfaceDump
	--
	shelllistcount = ARRAYSIZE(shelllist)
	_j=1
	_q=''
	_shellID=shellID
	SETDELIM(shelllist,'*')
	if reference0 {
		_j=TOINT(reference0)
	}
	if IsHShell(reference1) && !CanH{
		_q='当前关系还不允许激活这件衣服'
	}
	elseif reference1 && reference1!=shellID &&reference1==reference2{
		_q="\f[color,242,35,250]\![*]\q[确定 换装为%(reference1),OnChangeShellWithTalk,%(reference1)]\f[default]"
		_shellID=reference1
	}
	elseif reference1!=reference2{
		_q="没有激活这件衣服\n\&[hearts]\q[◇购买激活%(reference1)（￥5000）,OnBuyShell,%(reference1),%(_shellID)]"
		_shellID=reference1
	}
	_haveshellnum=0
	foreach shelllist; _i{
		if judgeshell(_i)=='0|' &&judgehaveshell(_i)=='-1'&&Userspitem[0][2]!=1
			_haveshellnum=_haveshellnum
		else
			_haveshellnum++
	}
	"\C\![quicksession,true]\c\![set,choicetimeout,0]\0\s[26]\b[2]橘花共有%(shelllistcount)件衣服哦"
	if shelllistcount!=_haveshellnum
		"，其中%(_haveshellnum)件已激活"
	"，%(username)想让橘花穿那件衣服呢？\n[140]"
	_surface_preview=''
	if (_t=judgeshell(_shellID))!='0|'{
		_Shellrand=ANY('0','1','2','3','5','7','20','21','22','23','26','30','32')
		_surface_preview="VAR_PATH/surface_dumps/%(_t)/surface%(_Shellrand).png"
		if !FILEEXIST(_surface_preview)
			_surface_preview=''
	}
	if !_surface_preview{
		if FILEEXIST('DATA_PATH/shell/surface_unknown.png')
			_surface_preview='DATA_PATH/shell/surface_unknown.png'
	}
	if _surface_preview
		"\_b[%(_surface_preview),40,30,--option=use_self_alpha]"
	_n=1
	_judge=''
	_haveshell=''
	_shelllist=''
	foreach shelllist; _i {
		if _n>=_j && _n<_j+15 {
			if judgeshell(_i)=='0|' &&judgehaveshell(_i)=='-1'&&Userspitem[0][2]!=1
				_judge='\f[color,192,192,192]'+_i+'\f[color,default]'
			else{
				_judge=_i
				_haveshell=_i
			}
			if shellID==_i
				_shelllist += "\f[color,240,45,100]\&[hearts]\f[default]\__q[OnFittingRoom,%(_j),%(_i),%(_haveshell)]%(_judge)\__q\n[120]"
			elseif _shellID==_i
				_shelllist += "\f[color,24,98,250]\&[hearts]\f[default]\__q[OnFittingRoom,%(_j),%(_i),%(_haveshell)]%(_judge)\__q\n[120]"
			else
				_shelllist += "\f[color,168,168,168]\&[hearts]\f[default]\__q[OnFittingRoom,%(_j),%(_i),%(_haveshell)]%(_judge)\__q\n[120]"
		}
		_n ++
	}
	_shelllist
	'\n[30]'
	if shelllistcount>15{
		if _j!=1
			"\q[<<上一页,OnFittingRoom,%(_j-15)]   "
		if _j+15<shelllistcount
			"\q[下一页>>,OnFittingRoom,%(_j+15)]"
	}
	'\n[140]'
	_q
	'\_l[0,-]\n[150]/
	\![*]\q[返回上一层,OnOpenMenu,1]\n/
	\![*]\q[查看可下载shell,OnUrlOpen,https://github.com/Taromati2/Taromati2/releases/tag/shell_list]\n/
	\![*]\q[取消,overchangeshell]/
	'
}
judgehaveshell{
	_a='-1'
	_HaveshellList='预设,預設,聖誕服裝,圣诞服装,万圣节,ハロウィン,旗袍,浴衣,夏服,競泳水着,白スク水,スク水,裸Yシャツ,拘束衣,水玉水着,夏服2,长裙,護士服裝,tail,猫耳,バニーガール,銀狐巫女,女仆装,女仆,キャミソール,背心,セーター,体操服,全裸'
	foreach _HaveshellList; _i {
		if _argv[0] == _i
			_a=_i
	}
	_a
}
judgeshell{
	_a='0|'
	SETDELIM(Haveshellinfo,'*')
	foreach Haveshellinfo; _i {
		if _argv[0] ==  _i[0,'|']
			_a=_i[1,'|']
	}
	_a
}
OnChangeShellTalk{
	case reference0 {
		when '预设','預設' {
			'还是穿平时的比较好吗？\w9'
		}
		//此处可以添加不同衣着的特殊对话
		//------------------------------------------------------------------------
		when '聖誕服裝', '圣诞服装'{
			if month == 12 && day == 24 {
				'\0\s[5]今天是圣诞前夜啊.\w9\w9是应该换上这套衣服的.\w9\w9\n'
			}
			elseif month == 12 && day == 25 {
				'\0\s[5]今天圣诞节啊……\w9\w9嗯,\w9橘花马上去换.\w9\w9\n'
			}
			else {
				"\0\s[20]%(username)为什么要橘花现在换上这件衣服呢?\w9\n\1\s[10]也许是穿上这件衣服的橘花显得比较可爱吧.\w9\w9\n\0\s[29]唔……\w9\w9\n"
			}
		}
		when '万圣节','ハロウィン' {
			if month == 10 && day == 31 {
				'\0\s[8]呃…\w9万圣节…\w9\w9换上相应的衣服…\w9\w9也是应该的啊…\w9\w9\n\1\s[11]告诉你,\w9其实橘花很怕那东西的\w9\0\s[9]哪有…\w9\w9把你装进南瓜灯里面.\w9\1\s[12]呜哇……\w9\w9\n\0'
			}
			else {
				"\0\s[20]%(username)为什么要橘花现在换上这件衣服呢?\w9\n\1\s[10]也许是穿上这件衣服的橘花显得比较可爱吧.\w9\w9\n\0\s[29]唔……\w9\w9\n"
			}
		}
		when '旗袍','チャイナ' {
			"\0\s[5]中国传统服饰哦,\w9%(username)应该很熟悉了吧\w9\w9"
		}
		when '浴衣' {
			"\0\s[23]这套是橘花洗完澡以后才穿的,\w9%(username)喜欢橘花这样穿的样子吗?\w9\w9"
		}
		when '夏服' {
			'\0\s[0]这套,\w9是白色的夏装哦\w9\w9\n因为容易弄脏,\w9\s[21]所以橘花很少穿啦\w9\w9\1\s[10]我对容易被什么弄脏很感兴趣\w9\w9\0'
		}
		when '競泳水着' {
			"\0\s[26]难道…\w9%(username)喜欢橘花在学校时穿的泳装?\w9\n\1\s[11]总是会有那种人的嘛,\w9可以理解\w9\w9\0"
		}
		when '白スク水','スク水','裸Yシャツ','拘束衣' {
			'\0\s[33]呜……\w9\w9好难为情的衣服啊\w9\n'
		}
		when '水玉水着' {
			"\0\s[9]明明在屋子里,\w9%(username)为什么要橘花穿这种衣服\w9\w9\n"
		}
		when '夏服2' {
			'\0\s[0]橘花夏天穿的衣服,\w9\w9\s[3]又轻又结实,\w9\w9\s[5]还不容易被血弄脏\w9\w9\n\1\s[13]说到什么地方去了啊\w9\w9\0'
		}
		when '长裙' {
			"\0\s[1]这套,\w9是橘花根据经常穿的正装自己改的说.\w9\w9\n/
			\s[26]不知道%(username)喜不喜欢\w9\w9\n\1\s[10]换上让他看看不就知道了吗\w9\w9\0"
		}
		when '護士服裝' {
			'\0\s[2]说起来,\w9\w9橘花\w9\s[5]也是学习过战场救护的哦\w9\n\1\s[10]没问你这个…\w9\w9\0'
		}
		when 'tail' {
			'\0\s[8]呃…\w9…\w9\w9斗和没问题吧.\w9\n\1\s[13]扯上我干什么\w9\w9\0'
		}
		when '猫耳','バニーガール','銀狐巫女' {
			'\0\s[8]这套…\w9\w9有点\w9\w9\s[21]难为情啊…\n\1\s[11]明明很期待的样子.\w9\w9\w9\0'
		}
		when '女仆装','女仆' {
			"\0\s[9]咦……\w9\w9原来%(username)是萌女仆的呀～\w9\w9\n可是…\w9\s[5]Ｈ是不行的唷～\w9\w9\n\n/
			\1\s[12]你想到哪里去了啊……\w9\w9\n\n\0那橘花去换衣服咯～\w9\1\s[11]好好听听别人说话啦！\w9\0"
		}
		when 'キャミソール','背心' {
			'\0\s[5]橘花很喜欢这件背心，夏天穿很舒服呢～\w9\w9'
		}
		when 'セーター' {
			'\0\s[5]很暖和的样子\w9\w9'
		}
		when '体操服' {
			"\0\s[26]难道…\w9%(username)喜欢橘花在学校时穿的体操服?\w9\n\1\s[11]总是会有那种人的嘛,\w9可以理解\w9\w9\0"
		}
		others {
			"\0\s[0]%(reference0)吗?\w7\n"
			--
			'\s[6]好的,'
			''
			--
			'\w7\s[26]知道了\n'
			--
			'\w5\s[-1]那橘花去换衣服了哦'
			'\w5\s[-1]橘花换衣服了哦'
			--
			'~'
			",%(username)不许偷看哈♪"
		}
	}
	--
	'\s[-1]\b[-1]\w9'
}
Select.overchangeshell{
	"\0不用换也行吗?\w6\n果然是%(shellID)好吗～"
}
On_installedshellname{
	shelllist = ''
	_i = 0
	while _i >= 0 {
		_shellname = NAMETOVALUE("reference%(_i)")
		if _shellname != '' {
			if _i > 0
				shelllist += '*'
			shelllist += _shellname[0]
			_i++
		}
		else
			_i = -1
	}
	shelllistcount = ARRAYSIZE(shelllist)
}
